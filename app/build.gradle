/*
 * Copyright 2020 CoviLights GbR
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.appdistribution'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply from: "$rootDir/ktlint.gradle"
apply from: "$rootDir/jacoco.gradle"
apply from: "$rootDir/detekt.gradle"

def releaseNote = "Commit: ${commitMessage()}\n\n" +
        "Branch: ${System.getenv('GITHUB_REF')}\n\n" +
        "Author: ${commitAuthor()}\n\n" +
        "Hash: ${commitShortHash()}"

def releaseTestersGroup = "qa, dev"

android {
    compileSdkVersion rootProject.ext.compile_sdk_version

    defaultConfig {
        applicationId "com.covilights"

        minSdkVersion rootProject.ext.min_sdk_version
        targetSdkVersion rootProject.ext.compile_sdk_version

        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            firebaseAppDistribution {
                releaseNotes = releaseNote
                groups = releaseTestersGroup
            }
        }
        debug {
            testCoverageEnabled true

            firebaseAppDistribution {
                releaseNotes = releaseNote
                groups = releaseTestersGroup
            }
        }
    }

    viewBinding {
        enabled = true
    }

    dataBinding {
        enabled = true
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    lintOptions {
        disable 'MissingTranslation'
    }
}

check.dependsOn ktlint

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.4.21"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2"

    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.legacy:legacy-support-v4:$androidx_legacy_support_v4"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-service:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$androidx_lifecycle_version"
    kapt "android.arch.lifecycle:common-java8:$androidx_lifecycle_version"
    testImplementation "androidx.arch.core:core-testing:$androidx_arch_version"

    implementation "com.google.android.material:material:1.2.1"

    testImplementation "junit:junit:4.13.1"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"

    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-core-ext:$koin_version"
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    implementation "org.koin:koin-androidx-fragment:$koin_version"
    testImplementation "org.koin:koin-test:$koin_version"

    implementation "com.jakewharton.timber:timber:$timber_version"

    implementation "androidx.navigation:navigation-fragment-ktx:2.3.2"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.2"

    implementation "com.mirhoseini.appsettings:appsettings:$appsettings_version"

    implementation "com.mohsenoid.closetome:closetome:$closetome_version"
}

apply plugin: 'com.google.gms.google-services'
